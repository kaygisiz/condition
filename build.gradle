plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'application'
}

ext {
    groupId = 'com.github.kaygisiz'
    artifactId = 'conditional-stream'
    latestVersion = '0.1'
    githubUrl = 'https://github.com/kaygisiz'
    projectUrl = githubUrl + '/conditional-stream'
}

group groupId
version latestVersion

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

mainClassName = 'com.github.kaygisiz.example.Main'

jar {
    exclude('com/github/kaygisiz/example/**')
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId groupId
            artifactId artifactId
            version latestVersion

            from components.java

            // create the sign pom artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username SONATYPE_USERNAME
                password SONATYPE_PASSWORD
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Create Conditional Stream of a Generic Type Object'
            name 'Conditional Stream'
            url projectUrl
            organization {
                name groupId
                url githubUrl
            }
            issueManagement {
                system 'GitHub'
                url projectUrl + '/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url projectUrl + '/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url projectUrl
                connection 'scm:git:git://github.com/kaygisiz/conditional-stream.git'
                developerConnection 'scm:git:ssh://git@github.com:kaygisiz/conditional-stream.git'
            }
            developers {
                developer {
                    name 'Kaygisiz'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}